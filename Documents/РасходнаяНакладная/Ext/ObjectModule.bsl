
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Движения.ОстаткиНоменклатуры.Записывать=Истина;
	Движения.Продажи.Записывать=Истина;
	
	Движения.Продажи.Записать();
	Движения.ОстаткиНоменклатуры.Записать();
	
	Движения.ОстаткиНоменклатуры.Записывать=Истина;
	Движения.Продажи.Записывать=Истина;

	
	//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
		|ПОМЕСТИТЬ ВТ_СписокНом
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|	И РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_СписокНом.Номенклатура КАК Номенклатура,
		|	ВТ_СписокНом.Количество КАК Количество,
		|	ВТ_СписокНом.Сумма КАК СуммаПрод,
		|	ОстаткиНоменклатурыОстатки.Партия КАК Партия,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
		|	ОстаткиНоменклатурыОстатки.Партия.МоментВремени КАК ПартияМоментВремени,
		|	ВТ_СписокНом.Номенклатура.Представление КАК НоменклатураПредставление
		|ИЗ
		|	ВТ_СписокНом КАК ВТ_СписокНом
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|				&МоментВремени,
		|				Номенклатура В
		|					(ВЫБРАТЬ
		|						ВТ_СписокНом.Номенклатура КАК Номенклатура
		|					ИЗ
		|						ВТ_СписокНом КАК ВТ_СписокНом)) КАК ОстаткиНоменклатурыОстатки
		|		ПО ВТ_СписокНом.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПартияМоментВремени УБЫВ
		|ИТОГИ
		|	МАКСИМУМ(Количество),
		|	МАКСИМУМ(СуммаПрод),
		|	СУММА(КоличествоОстаток),
		|	СУММА(СуммаОстаток)
		|ПО
		|	Номенклатура
		|АВТОУПОРЯДОЧИВАНИЕ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	РасходнаяНакладнаяСписокНоменклатуры.Количество КАК Количество,
		|	РасходнаяНакладнаяСписокНоменклатуры.Сумма КАК Сумма
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)
		|	И РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка";
	
	
	УчПол = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(Дата);
	
	Если УчПол.МетодСписания = Перечисления.УчетнаяПолитика.ФИФО Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПартияМоментВремени УБЫВ" , "ПартияМоментВремени");
	КонецЕсли; 
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", Дата);
	
	
	Блокировка = Новый БлокировкаДанных;	 	 
	Элемент = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");	 	 
	Элемент.Режим = РежимБлокировкиДанных.Исключительный;	 	 
	Элемент.ИсточникДанных = СписокНоменклатуры;	 	 
	Элемент.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура"); 	 	 
	
	Блокировка.Заблокировать();	 	 
	
	
	РезультатЗапросаМассив = Запрос.ВыполнитьПакет();
	
	РезультатЗапросаТов = РезультатЗапросаМассив[1];
	РезультатЗапросаУсл = РезультатЗапросаМассив[2];
	
	
	Движения.Продажи.Загрузить(РезультатЗапросаУсл.Выгрузить());
	
	
	
	
	ВыборкаНоменклатура = РезультатЗапросаТов.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстаток Тогда
			Отказ = истина;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не хватает " + ВыборкаНоменклатура.НоменклатураПредставление + ". Продается " + ВыборкаНоменклатура.Количество + ", а в остатке " + ВыборкаНоменклатура.КоличествоОстаток;
			Сообщение.Сообщить();
			
			Продолжить;
		КонецЕсли;	
			
		КолСписать = ВыборкаНоменклатура.Количество;
		
		Выборка = ВыборкаНоменклатура.Выбрать();
		
		Пока Выборка.Следующий() и КолСписать > 0 Цикл
			
			Списать = Мин(КолСписать, Выборка.КоличествоОстаток);	
			
			Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
			Движение.Период 		= Дата;			
			Движение.Номенклатура 	= Выборка.Номенклатура;
			Движение.Партия 		= Выборка.Партия;
			Движение.Количество 	= Списать;
			
			Если Списать = Выборка.КоличествоОстаток Тогда
				Движение.Сумма 		= Выборка.СуммаОстаток;
			иначе
				Движение.Сумма 		= Выборка.СуммаОстаток * Списать / Выборка.КоличествоОстаток;
			КонецЕсли; 
			
			КолСписать = КолСписать - Списать;
		КонецЦикла; 
		
		Движение = Движения.Продажи.Добавить();
		Движение.Период 		= Дата;
		Движение.Номенклатура 	= ВыборкаНоменклатура.Номенклатура;
		Движение.Количество 	= ВыборкаНоменклатура.Количество;
		Движение.Сумма 			= ВыборкаНоменклатура.СуммаПрод;
		
	КонецЦикла;
	

	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА

КонецПроцедуры
